%!TEX root = fusion.tex

\begin{figure*}
\begin{minted}{Haskell}
data Combination a b =
    B (b -> b -> b)
  | L (b -> Bool, b -> b -> b, b -> Graph a -> b)
  | R (Graph a -> Bool, Graph a -> b -> b, Graph a -> Graph a -> b)

switchCombi :: Combination a b -> b -> b -> Graph a -> Graph a -> b
switchCombi (B f)            = \a b _ _ -> f a b
switchCombi (L (pred,c1,c2)) = \a b x _ -> if pred b then c1 a b else c2 x b
switchCombi (R (pred,c1,c2)) = \a b _ x -> if pred a then c1 a b else c2 a x
  
paragraphR :: b -> (a -> b) -> Combination a b -> Combination a b -> Graph a -> b
paragraphR e v o c = paragraph e v (switchCombi o) (switchCombi c)
{-# INLINE [0] paragraphR #-}

apply2LR :: (Graph c -> Graph a) -> Combination a b -> Combination c b
apply2LR _ (B a) = B a
apply2LR f (L (a,b,c)) = L (a,b,\x y -> c (f x) y)
apply2LR f (R (a,b,c)) = R (a,b,\x y -> c x (f y))

composeR :: (b -> c) -> (a -> b) -> a -> c
composeR = (.)
{-# INLINE [0] composeR #-}

type Paragraph a = forall b. b -> (a -> b) -> Combination a b -> Combination a b -> b
buildR :: Paragraph a -> Graph a
buildR g = g Empty Vertex (B Overlay) (B Connect)
{-# INLINE [1] buildR #-}

-- Bind already in its "buildR" form (to avoid a rewriting circle with "buildR/bindR")
buildBindR :: (a -> Graph b) -> Graph a -> Graph b
buildBindR f g = 
  buildR (\e v o c -> 
    paragraphR e (composeR (paragraphR e v o c) f) (apply2LR bind2R o) (apply2LR bind2R c) 
    g)
  where
    -- bind without rewrite rules (to avoid a rewriting circle with "buildR/bindR")
    bind2R = foldg Empty f Overlay Connect
    
{-# RULES
"buildR/bindR" forall (f::a -> Graph b) g.
bindR g f = buildR 
  (\e v o c -> 
    paragraphR e (composeR (paragraphR e v o c) f) (apply2LR (buildBindR f) o) (apply2LR (buildBindR f) c)
    g)

"foldg/buildR" forall e v o c (g :: Paragraph a). foldg e v o c (buildR g) = g e v (B o) (B c)

"paragraph/buildR" forall e v o c (g :: Paragraph a). paragraphR e v o c (buildR g) = g e v o c

-- Fuse composeR's. This occurs when two adjacent 'bindR' were rewritted into their buildR form.
"bindR/bindR" forall c f g. composeR (composeR c f) g = composeR c (f.g)

-- Rewrite identity (which can appear in the rewriting of bindR) to a much efficient one
"foldg/id" paragraphR Empty Vertex (B Overlay) (B Connect) = id
 #-}
\end{minted}
\caption{Paramorphism fusion for algebraic graphs}
\label{fig:parafus}
\end{figure*}
